plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.4-SNAPSHOT" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()
    }

    repositories {
        maven {
            name "NeoForged"
            url "https://maven.neoforged.net/releases/"
        }
    }

    processResources {
        def expandProps = [
                "name": rootProject.mod_name,
                "version": rootProject.mod_version,
                "description": rootProject.mod_description,
                "id": rootProject.mod_id,
                "homepage_url": rootProject.homepage_url,
                "sources_url": rootProject.sources_url,
                "issue_tracker_url": rootProject.issue_tracker_url,
                "license": rootProject.license,
                "minecraft_version": rootProject.minecraft_version,
                "minecraft_version_range": rootProject.minecraft_version_range,
				"minecraft_version_range_fabric": rootProject.minecraft_version_range_fabric,
                "fabric_loader_version": rootProject.fabric_loader_version,
                "fabric_version": rootProject.fabric_api_version,
                "neoforge_version": rootProject.neoforge_version,
                "neoforge_version_range": rootProject.neoforge_version_range
        ]

        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', '*.mixins.json']) {
            expand expandProps
        }

        inputs.properties(expandProps)
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = rootProject.archives_base_name
    }

    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
